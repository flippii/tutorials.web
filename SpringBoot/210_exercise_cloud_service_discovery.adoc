== Exercise: Spring Cloud service discovery

Currently the gateway just points to the user service directly and
we do not see any real benefit of having this facade in front of the user service.

One huge benefit of using micro service architectures is that additional services can be spawned, which can be load balanced, if one service is too busy.

For service discovery a generic name for services has to be applied. This can be done by using the _spring.application.name_ property.

For the user project the _application.properties_ file should look like this:

[source,java]
----
server.port=8081
spring.application.name=user
----

We want to use Netflix Eureka for service discovery and therefore have to add a dependency to `org.springframework.cloud:spring-cloud-starter-netflix-eureka-client`.

The _build.gradle_ should be adjusted to look like this:

[source,java]
----
buildscript {
	ext {
		springBootVersion = '2.1.0.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.vogella'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/milestone" } <1>
}

ext {
	springCloudVersion = 'Greenwich.M1' <2>
}

dependencies {
	implementation('org.springframework.boot:spring-boot-starter-actuator')
	implementation('org.springframework.boot:spring-boot-starter-data-mongodb-reactive')
	implementation('org.springframework.boot:spring-boot-starter-webflux')
	implementation('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client') <3>
	compileOnly('org.projectlombok:lombok')
	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation('de.flapdoodle.embed:de.flapdoodle.embed.mongo')
	testImplementation('io.projectreactor:reactor-test')
}

dependencyManagement { <4>
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}
----

<1> The milestone repositories have to be added, because the Spring Cloud Greenwich version is published as milestone for now.
<2> Store the Spring Cloud version in a ext project property
<3> Add the org.springframework.cloud:spring-cloud-starter-netflix-eureka-client dependency
<4> dependencyManagement for Spring Cloud dependencies has to be applied

Now refresh the Gradle dependencies by clicking menu:Gradle[Refresh Gradle Project] and add the `@EnableDiscoveryClient` to the `Application` class and the user project is ready to be discovered by Netflix Eureka.
